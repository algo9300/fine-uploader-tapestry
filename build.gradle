apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'war'
apply plugin:  'groovy'

group = 'rnicholus'
version = '1.0-SNAPSHOT'
webAppDirName = 'web'

sourceCompatibility = 1.5
targetCompatibility = 1.5

repositories {
	mavenCentral()
}

configurations {
	all*.exclude group: 'commons-logging', module: 'commons-logging'
	all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	all*.exclude group: 'log4j', module: 'log4j'
	all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet', ext: 'orbit'  //@see http://stackoverflow.com/questions/9889674/sbt-jetty-and-servlet-3-0
	all*.exclude group: 'javax.servlet', module: 'servlet-api', version: '2.5'
}

dependencies {
    // Logging
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.7'

    ext.slf4jVersion = '1.7.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: slf4jVersion
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jVersion

    // Utility libraries
    compile group: 'org.apache.commons', name: 'commons-fileuploader', version: '1.2.2'

	// Tapestry 5
	ext.tapestryVersion = '5.3.6'
	compile group: 'org.apache.tapestry', name: 'tapestry5-annotations', version: tapestryVersion
	compile group: 'org.apache.tapestry', name: 'tapestry-core', version: tapestryVersion
	runtime group: 'org.apache.tapestry', name: 'tapestry-func', version: tapestryVersion
	compile group: 'org.apache.tapestry', name: 'tapestry-ioc', version: tapestryVersion
	runtime group: 'org.apache.tapestry', name: 'tapestry-json', version: tapestryVersion

	// Local Jetty -- http://wiki.eclipse.org/Jetty/Tutorial/Embedding_Jetty
	testCompile group: 'org.eclipse.jetty.aggregate', name: 'jetty-all', version: '8.1.3.v20120416'
	testCompile group: 'org.eclipse.jetty', name: 'jetty-util', version: '8.1.3.v20120416'
    testCompile group: 'org.glassfish.web', name: 'javax.servlet.jsp', version: '2.2.3'
    testCompile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.3'

    // Testing
	testCompile group: 'junit', name: 'junit', version: '4.9'
	testCompile group: 'org.spockframework', name: 'spock-core', version: '0.6-groovy-1.8'
	testCompile group: 'org.spockframework', name: 'spock-tapestry', version: '0.6-groovy-1.8'
	testCompile group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '1.2'
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy {
            srcDirs = files('src/main')
            resources.srcDirs = files('src/main/resources')
        }
    }

    test {
        groovy.srcDirs = files('src/test')
        resources.srcDirs = files('src/test/resources')
    }
}

war {
    dependsOn = [ 'lessCss' ]

    baseName = 'fine-uploader-tapestry'

    // simplify when GRADLE-1360 is resolved; http://issues.gradle.org/browse/GRADLE-1360
    // remove classes from the classpath
    classpath = configurations.runtime - configurations.providedRuntime

    // add them in explicitly, with the filtering applied
    webInf {
        into('classes') {
            from (sourceSets.main.output.classesDir)
            from (sourceSets.main.output.resourcesDir)
            exclude 'logback-test.xml' // this is only for development; logback.xml is used in production
        }
    }
}

task compile(description: 'Alias for "classes" task', dependsOn: classes)

tasks.idea {
	dependsOn = [ ideaModule ]
}

idea.module.iml {
    beforeMerged { module ->
        module.dependencies.clear()
    }
}

def ideClasspath()
{
	return sourceSets.test.runtimeClasspath;
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0'
}
